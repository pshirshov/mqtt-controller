{
  config,
  lib,
  pkgs,
  ...
}:

# Unified MQTT lighting controller (replaces bento `mqtt-automation` rule
# engine + python `hue-setup` z2m provisioner). One binary, two units:
#   mqtt-controller-provision.service  (oneshot, after z2m + mosquitto)
#   mqtt-controller.service            (long-running, after provision)
# Both consume one JSON config rendered from
# `smind.services.mqtt-controller.config`, matching the `Config` schema in
# `crates/mqtt-controller/src/config/mod.rs`.

let
  cfg = config.smind.services.mqtt-controller;
  yaml = pkgs.formats.json { };
in
{
  options.smind.services.mqtt-controller = {
    enable = lib.mkEnableOption "Unified zigbee2mqtt provisioner + runtime controller";

    package = lib.mkOption {
      type = lib.types.package;
      default = pkgs.mqtt-controller;
      defaultText = lib.literalExpression "pkgs.mqtt-controller";
      description = "mqtt-controller package to run.";
    };

    config = lib.mkOption {
      type = lib.types.attrsOf lib.types.anything;
      default = { };
      description = ''
        Unified config consumed by both the provisioner and the daemon.
        Rendered as JSON. The structure must match the `Config` type in
        `crates/mqtt-controller/src/config/mod.rs`:

        ```
        {
          name_by_address = { "0x..." = "hue-s-foo"; ... };
          devices = {
            "hue-l-foo" = { kind = "light"; ieee_address = "0x..."; };
            "hue-ms-foo" = {
              kind = "motion-sensor";
              ieee_address = "0x...";
              occupancy_timeout_seconds = 60;
              options = { occupancy_timeout = 60; motion_sensitivity = "high"; };
            };
            ...
          };
          rooms = [
            {
              name = "kitchen-cooker";
              group_name = "hue-lz-kitchen-cooker";
              id = 15;
              members = [ "hue-l-cooker-bottom/11" ... ];
              parent = "kitchen-all";
              scenes = { ... };
              off_transition_seconds = 0.8;
            }
            ...
          ];
          motion_rules = [ {
            name = "kitchen-motion";
            sensors = [ "hue-ms-foo" ];
            mode = "on-off";
            scenes = { ... };
            targets_by_slot = { day = { group = "kitchen-cooker"; }; };
            off_transition_seconds = 0.8;
            off_cooldown_seconds = 30;
            max_illuminance = 15;
          } ];
          defaults = {
            cycle_window_seconds = 1.0;
            wall_switch = {
              brightness_step = 25;
              brightness_step_transition_seconds = 0.2;
              brightness_move_rate = 40;
            };
          };
          # Optional heating subsystem:
          heating = {
            zones = [ {
              name = "floor-bathroom";
              relay = "bosch-wt-bathroom";   # wall-thermostat device
              trvs = [
                { device = "bosch-trv-bath-1"; schedule = "bathroom"; }
              ];
            } ];
            schedules.bathroom = {
              monday = [
                { start = "00:00"; end = "06:00"; temperature = 18.0; }
                { start = "06:00"; end = "22:00"; temperature = 22.0; }
                { start = "22:00"; end = "24:00"; temperature = 18.0; }
              ];
              # tuesday..sunday required (same structure)
            };
            pressure_groups = [ ];
            heat_pump = { min_cycle_seconds = 300; min_pause_seconds = 180; };
            open_window = { detection_minutes = 20; inhibit_minutes = 80; };
          };
        }
        ```

        Consumers may translate a higher-level Nix room/device/scene model
        into the flat JSON shape the controller expects.
      '';
      example = lib.literalExpression "{ rooms = [ ]; }";
    };

    mqtt = {
      host = lib.mkOption {
        type = lib.types.str;
        default = "localhost";
        description = "MQTT broker hostname.";
      };

      port = lib.mkOption {
        type = lib.types.port;
        default = 1883;
        description = "MQTT broker port.";
      };

      user = lib.mkOption {
        type = lib.types.str;
        default = "mqtt";
        description = "MQTT username.";
      };

      passwordFile = lib.mkOption {
        type = lib.types.path;
        description = ''
          Path to a file containing the MQTT password (one line). Read by
          systemd's LoadCredential so the daemon process never sees the
          plain file path.
        '';
      };
    };

    timezone = lib.mkOption {
      type = lib.types.str;
      default = "UTC";
      example = "Europe/Amsterdam";
      description = ''
        IANA timezone for the daemon's time-of-day slot dispatch
        (day/night scene cycles). Should match the host's local time.
      '';
    };

    location = {
      latitude = lib.mkOption {
        type = lib.types.nullOr lib.types.float;
        default = null;
        example = 53.35;
        description = "Latitude for sunrise/sunset calculations. Required when schedules use sun-relative expressions.";
      };
      longitude = lib.mkOption {
        type = lib.types.nullOr lib.types.float;
        default = null;
        example = -6.26;
        description = "Longitude for sunrise/sunset calculations. Required when schedules use sun-relative expressions.";
      };
    };

    web = {
      enable = lib.mkEnableOption "Web dashboard for the mqtt-controller daemon";

      port = lib.mkOption {
        type = lib.types.port;
        default = 8780;
        description = "Port for the web dashboard HTTP/WebSocket server.";
      };

      openFirewall = lib.mkOption {
        type = lib.types.bool;
        default = false;
        description = "Whether to open the web dashboard port in the firewall.";
      };
    };

    auditLog = {
      enable = lib.mkEnableOption ''
        Persistent audit log of decision-log entries. When enabled, the
        daemon records every processed event that produced visible
        effects or captured decision traces to a Turso (pure-Rust
        SQLite) database. The web dashboard reads it to render the
        per-entity log popup. Requires `web.enable = true` to be useful
        — without the dashboard there is no consumer.
      '';

      path = lib.mkOption {
        type = lib.types.str;
        default = "/var/lib/mqtt-controller/audit.db";
        description = ''
          Filesystem path of the audit-log database file. The default
          places it inside the daemon's `StateDirectory`, which is
          owned by the dynamic systemd user.
        '';
      };

      retentionDays = lib.mkOption {
        type = lib.types.ints.positive;
        default = 30;
        description = "Rows older than this many days are deleted by the retention sweep.";
      };

      perEntityMaxRows = lib.mkOption {
        type = lib.types.ints.positive;
        default = 2000;
        description = ''
          Per-entity row cap. The retention sweep keeps at most this
          many rows per entity, even if some fall outside the time
          window. Guards against a noisy entity pushing out a quiet
          entity's history.
        '';
      };

      flushIntervalMs = lib.mkOption {
        type = lib.types.ints.positive;
        default = 5000;
        description = "Writer flush interval in milliseconds.";
      };

      flushMaxRows = lib.mkOption {
        type = lib.types.ints.positive;
        default = 100;
        description = "Writer flush cap: commit fires once this many rows are buffered.";
      };

      sweepIntervalSecs = lib.mkOption {
        type = lib.types.ints.positive;
        default = 3600;
        description = "Retention sweep interval in seconds.";
      };
    };
  };

  config = lib.mkIf cfg.enable (
    let
      locationAttr = lib.optionalAttrs (cfg.location.latitude != null && cfg.location.longitude != null) {
        location = { inherit (cfg.location) latitude longitude; };
      };
      auditLogAttr = lib.optionalAttrs cfg.auditLog.enable {
        audit_log = {
          path = cfg.auditLog.path;
          retention_days = cfg.auditLog.retentionDays;
          per_entity_max_rows = cfg.auditLog.perEntityMaxRows;
          flush_interval_ms = cfg.auditLog.flushIntervalMs;
          flush_max_rows = cfg.auditLog.flushMaxRows;
          sweep_interval_secs = cfg.auditLog.sweepIntervalSecs;
        };
      };
      mergedConfig = cfg.config // locationAttr // auditLogAttr;
      configFile = yaml.generate "mqtt-controller.json" mergedConfig;

      # `--verbose` is a clap global flag, so it must precede the subcommand.
      commonArgs = lib.concatStringsSep " " [
        "--config ${configFile}"
        "--mqtt-host ${cfg.mqtt.host}"
        "--mqtt-port ${toString cfg.mqtt.port}"
        "--mqtt-user ${cfg.mqtt.user}"
        "--mqtt-password-file \"$CREDENTIALS_DIRECTORY/mqtt-password\""
      ];
    in
    {
      environment.systemPackages = [ cfg.package ];

      networking.firewall.allowedTCPPorts = lib.optionals (cfg.web.enable && cfg.web.openFirewall) [
        cfg.web.port
      ];

      # Provisioner: re-runs whenever the rendered JSON changes (restartTriggers).
      systemd.services.mqtt-controller-provision = {
        description = "Apply declarative zigbee2mqtt groups + scenes from Nix config";
        wantedBy = [ "multi-user.target" ];
        # After=zwave-js-ui orders us behind the unit when it exists; the
        # driver can still report "not connected" once the unit is up, so
        # the binary treats Z-Wave reconcile as best-effort.
        after = [
          "zigbee2mqtt.service"
          "mosquitto.service"
          "zwave-js-ui.service"
          "network-online.target"
        ];
        wants = [
          "zigbee2mqtt.service"
          "mosquitto.service"
          "network-online.target"
        ];
        restartTriggers = [ configFile ];
        before = [ "mqtt-controller.service" ];
        script =
          let
            z2mPort = config.smind.services.zigbee2mqtt.port;
            z2mWsUrl = "ws://localhost:${toString z2mPort}/api";
          in
          ''
            exec ${cfg.package}/bin/mqtt-controller --verbose provision ${commonArgs} \
              --z2m-ws-url ${z2mWsUrl}
          '';
        unitConfig = {
          # Re-queues a daemon start job once the provisioner succeeds,
          # covering the case where provision only succeeds on a later retry.
          Upholds = "mqtt-controller.service";
        };
        serviceConfig = {
          Type = "oneshot";
          RemainAfterExit = true;
          LoadCredential = "mqtt-password:${cfg.mqtt.passwordFile}";
          Restart = "on-failure";
          RestartSec = 5;
          DynamicUser = true;
        };
      };

      # Force provisioner: adds `--force-options` to rewrite every per-device
      # option even when z2m reports it already applied. Escape hatch for
      # devices whose reported state diverges from physical state (e.g. Sonoff
      # `inching_control`, `overload_protection`). Not wanted by any target;
      # run on demand: systemctl start mqtt-controller-force-provision.service
      systemd.services.mqtt-controller-force-provision = {
        description = "Force-rewrite zigbee2mqtt device options (bypasses state-cache dedup)";
        after = [
          "zigbee2mqtt.service"
          "mosquitto.service"
          "network-online.target"
        ];
        wants = [
          "zigbee2mqtt.service"
          "mosquitto.service"
          "network-online.target"
        ];
        script =
          let
            z2mPort = config.smind.services.zigbee2mqtt.port;
            z2mWsUrl = "ws://localhost:${toString z2mPort}/api";
          in
          ''
            exec ${cfg.package}/bin/mqtt-controller --verbose provision ${commonArgs} \
              --z2m-ws-url ${z2mWsUrl} \
              --force-options
          '';
        serviceConfig = {
          Type = "oneshot";
          LoadCredential = "mqtt-password:${cfg.mqtt.passwordFile}";
          DynamicUser = true;
        };
      };

      # Daemon. After= the oneshot so a successful provision runs first;
      # Upholds= on the provisioner starts us if we came up earlier.
      # Do not Requires= the oneshot: a Z-Wave getNodes blip (or any
      # later provision failure) would otherwise take lighting down and
      # start-limit would keep it down.
      systemd.services.mqtt-controller = {
        description = "MQTT lighting runtime controller (replaces bento mqtt-automation)";
        wantedBy = [ "multi-user.target" ];
        after = [
          "mqtt-controller-provision.service"
          "mosquitto.service"
          "network-online.target"
        ];
        wants = [
          "mosquitto.service"
          "network-online.target"
        ];
        restartTriggers = [ configFile ];
        environment = {
          TZ = cfg.timezone;
        };
        # `--verbose` on for now: logs every published command with its
        # state-machine branch. Drop once the runtime is stable.
        script =
          let
            z2mPort = config.smind.services.zigbee2mqtt.port;
            z2mWsUrl = "ws://localhost:${toString z2mPort}/api";
            # zwave-js-server embedded in ZJS-UI on port 3000. WebSocket +
            # JSON-RPC, see src/daemon/zwave_server.rs.
            zwaveWsUrl = "ws://localhost:3000";
          in
          ''
            exec ${cfg.package}/bin/mqtt-controller --verbose daemon ${commonArgs} \
              --z2m-ws-url ${z2mWsUrl} \
              --zwave-ws-url ${zwaveWsUrl} \
              --timezone ${cfg.timezone} \
              ${lib.optionalString cfg.web.enable "--web-port ${toString cfg.web.port} --web-assets-dir ${cfg.package}/share/mqtt-controller/web --web-history-db /var/lib/mqtt-controller/heating-history.db"}
          '';
        serviceConfig = {
          Type = "simple";
          Restart = "always";
          RestartSec = 5;
          LoadCredential = "mqtt-password:${cfg.mqtt.passwordFile}";
          DynamicUser = true;
          # Audit log lives here; set unconditionally so toggling
          # `auditLog.enable` needs no service-config change.
          StateDirectory = "mqtt-controller";
          StateDirectoryMode = "0750";
        };
      };
    }
  );
}
